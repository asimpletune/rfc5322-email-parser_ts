addr_spec       :=  local_part=local_part '@' domain=domain
local_part      :=   dot_atom=dot_atom | quoted_string=quoted_string | obs_local_part=obs_local_part
domain          :=   dot_atom=dot_atom | domain_literal=domain_literal | obs_domain=obs_domain
domain_literal  :=   CFWS? '\[' {FWS? dtext=dtext}* FWS? ']' CFWS?

// Printable US-ASCII characters not including "[", "]", or "\""
dtext           := '[!"#$%&\'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ]' | '[^_`abcdefghijklmnopqrstuvwxyz{|}~]' | obs_dtext
FWS := {{WSP* CRLF}? WSP+} |  obs_FWS
CFWS            :=   {{FWS? comment}+ FWS?} | FWS
dot_atom        :=   CFWS? dot_atom_text=dot_atom_text CFWS?
dot_atom_text   :=   head_atext=atext+ {'\.' tail_atext=atext+}*

// Printable US-ASCII characters not including specials.  Used for atoms.
atext             :=  atext='[A-Za-z0-9!#$%&\x27*+\-\/=?^_`{|}~]'
quoted_string   :=   CFWS?
                       DQUOTE {FWS? qcontent}* FWS? DQUOTE
                       CFWS?
obs_local_part  :=   word {'\.' word}*
obs_domain      :=   head_atom=atom {'\.' tail_atom=atom}*
obs_dtext       :=   obs_NO_WS_CTL | quoted_pair
obs_FWS         :=   WSP+ {CRLF WSP+}*
comment         :=   '\(' {FWS? ccontent}* FWS? '\)'
qcontent        :=   qtext | quoted_pair
word            :=   atom | quoted_string
atom            :=   CFWS? atext+ CFWS?

// US-ASCII control characters that do not include the carriage return, line feed, and white space characters
obs_NO_WS_CTL   :=   '[\x01\x02\x03\x04\x05\x06\x07\b]' | '\x0B' | '\f' | '[\x0E\x0F\x10\x11\x12\x13\x14\x15\x16\x17\x18\x19\x1A\x1B\x1C\x1D\x1E\x1F]' | '\x7F'

quoted_pair     :=   {'\\' {VCHAR | WSP}} | obs_qp
ccontent        :=   ctext | quoted_pair | comment

// Printable US-ASCII characters not including "\" or the quote character
qtext           :=   '!' | '[#$%&\'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]' | ']^_`abcdefghijklmnopqrstuvwxyz{|}~' | obs_qtext //"
obs_qp          :=   '\\' {'\x00' | obs_NO_WS_CTL | LF | CR}
ctext           :=  '[!"#$%&\']' | '[*+,-\./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]' | '[]^_`abcdefghijklmnopqrstuvwxyz{|}~]' | obs_ctext
obs_qtext       :=   obs_NO_WS_CTL
obs_ctext       :=   obs_NO_WS_CTL
WSP             := '\s'
CRLF            := '\r\n'
LF              := '\n'
CR              := '\r'
DQUOTE          := '"'
VCHAR          :=  '[\x21-\x7E]'