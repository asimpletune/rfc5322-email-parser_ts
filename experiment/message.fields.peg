---
import { makeLiteral } from './foo'
---
start := message
CR := '\x0D'
CRLF := '\r\n'
DIGIT := '\d'
TWO_DIGIT := '\d\d'
FOUR_DIGIT := '\d\d\d\d'
DQUOTE := '\x22'
HTAB := '\x09'
LF := '\x0A'
SP := '\x20'
VCHAR := '[\x21-\x7E]'
WSP := SP | HTAB
quoted_pair := A={ B='\\' C={ D=VCHAR | WSP } } | F=obs_qp
FWS := { { WSP* CRLF }? WSP+ } | obs_FWS
ctext := A='[\x21-\x27]' | B='[\x2a-\x5b]' | C='[\x5d-\x7e]' | D=obs_ctext
ccontent := A=ctext | B=quoted_pair | C=comment
comment := A='\(' B={ FWS? D=ccontent }* FWS? F='\)'
CFWS := A={ B={ FWS? D=comment }+ FWS? } | FWS
atext := '[A-Za-z0-9!#$%&\x27\*\+\-\/=?^_`{|}~]'
atom := CFWS? B=atext+ CFWS?
dot_atom_text := A=atext+ B={ C='\.' D=atext+ }*
dot_atom := A=CFWS? B=dot_atom_text C=CFWS?
specials := '\(' | '\)' | '[<>]' | '\[' | '\]' | '[:;@]' | '\\' | ',' | '\.' | DQUOTE
qtext := '\x21' | '[\x23-\x5b]' | '[\x5d-\x7e]' | obs_qtext
qcontent := qtext | quoted_pair
quoted_string := CFWS? B=DQUOTE C={ FWS? E=qcontent }* FWS? G=DQUOTE CFWS?
word := atom | quoted_string
phrase := word+ | obs_phrase
unstructured := A={ B={ FWS? D=VCHAR }* WSP* } | F=obs_unstruct
date_time := A={ B=day_of_week C=',' }? D=date E=time CFWS?
day_of_week := A={ FWS? C=day_name } | D=obs_day_of_week
day_name := 'Mon' | 'Tue' | 'Wed' | 'Thu' | 'Fri' | 'Sat' | 'Sun'
date := A=day B=month C=year
day := A={ FWS? C=DIGIT D=DIGIT? FWS } | F=obs_day
month := 'Jan' | 'Feb' | 'Mar' | 'Apr' | 'May' | 'Jun' | 'Jul' | 'Aug' | 'Sep' | 'Oct' | 'Nov' | 'Dec'
year := A={ B=FWS C=FOUR_DIGIT D=DIGIT* E=FWS } | F=obs_year
time := A=time_of_day B=zone
time_of_day := A=hour B=':' C=minute D={ E=':' F=second }?
hour := A=TWO_DIGIT | B=obs_hour
minute := A=TWO_DIGIT | B=obs_minute
second := A=TWO_DIGIT | B=obs_second
zone := A={ FWS C={ D='\+' | E='\-' } F=FOUR_DIGIT } | G=obs_zone
address := mailbox | group
mailbox := name_addr | addr_spec
name_addr := A=display_name? B=angle_addr
angle_addr := CFWS? B='<' C=addr_spec D='>' CFWS? | F=obs_angle_addr
group := A=display_name B=':' C=group_list? D=';' CFWS?
display_name := phrase
mailbox_list := A={ B=mailbox C={ D=',' E=mailbox }* } | F=obs_mbox_list
address_list := A={ B=address C={ D=',' E=address }* } | F=obs_addr_list
group_list := mailbox_list | CFWS | obs_group_list
addr_spec := A=local_part B='@' C=domain
local_part := dot_atom | quoted_string | obs_local_part
domain := dot_atom | domain_literal | obs_domain
domain_literal := CFWS? B='\[' C={ FWS? E=dtext }* FWS? G='\]' CFWS?
dtext := '[\x21-\x5a]' | '[\x5e-\x7e]' | obs_dtext
message := A={ fields | obs_fields } D={ CRLF F=body }?
body := A={ B={ C=_998text CRLF }* E=_998text } | F=obs_body
text := '[\x01-\x09]' | '\x0B' | '\x0C' | '[\x0E-\x7f]'
_998text := '[\x01-\x09\x0B\x0C\x0E-\x7F]{998,}'
fields := A={ B=trace C=optional_field* | D={ resent_date | resent_from | resent_sender | resent_to | resent_cc | resent_bcc | resent_msg_id }* }* L={ orig_date | from | sender | reply_to | to | cc | bcc | message_id | in_reply_to | references | subject | comments | keywords | optional_field }*
orig_date := A='Date' B=':' C=date_time CRLF
from := A='From' B=':' C=mailbox_list CRLF
sender := A='Sender' B=':' C=mailbox CRLF
reply_to := A='Reply_To' B=':' C=address_list CRLF
to := A='To' B=':' C=address_list CRLF
cc := A='Cc' B=':' C=address_list CRLF
bcc := A='Bcc' B=':' C={ address_list | CFWS }? CRLF
message_id := A='Message-ID' B=':' C=msg_id CRLF
in_reply_to := A='In-Reply-To' B=':' C=msg_id+ CRLF
references := A='References' B=':' C=msg_id+ CRLF
msg_id := CFWS? B='<' C=id_left D='@' E=id_right '>' G=CFWS?
id_left := dot_atom_text | obs_id_left
id_right := dot_atom_text | no_fold_literal | obs_id_right
no_fold_literal := A='\[' B=dtext* C='\]'
subject := A='Subject' B=':' C=unstructured CRLF
comments := A='Comments' B=':' C=unstructured CRLF
keywords := A='Keywords' B=':' C=phrase D={ E=',' F=phrase }* CRLF
resent_date := A='Resent-Date' B=':' C=date_time CRLF
resent_from := A='Resent-From' B=':' C=mailbox_list CRLF
resent_sender := A='Resent-Sender' B=':' C=mailbox CRLF
resent_to := A='Resent-To' B=':' C=address_list CRLF
resent_cc := A='Resent-Cc' B=':' C=address_list CRLF
resent_bcc := A='Resent-Bcc' B=':' C={ D=address_list | CFWS }? CRLF
resent_msg_id := A='Resent-Message_ID' B=':' C=msg_id CRLF
trace := A=return_path? B=received+
return_path := A='Return-Path' B=':' C=path CRLF
path := angle_addr | { CFWS? '<' CFWS '>' CFWS? }
received := A='Received' B=':' C=received_token* D=';' E=date_time CRLF
received_token := word | angle_addr | addr_spec | domain
optional_field := A=field_name B=':' C=unstructured CRLF
field_name := ftext+
ftext := '[\x21-\x39]' | '[\x3b-\x7e]'
obs_NO_WS_CTL := '[\x01-\x08]' | '\x0B' | C='\x0C' | D='[\x0E-\x1F]' | E='\x7F'
obs_ctext := obs_NO_WS_CTL
obs_qtext := obs_NO_WS_CTL
obs_utext := '\x00' | obs_NO_WS_CTL | VCHAR
obs_qp := A='\\' B={ '\x00' | obs_NO_WS_CTL | LF | CR }
obs_body := A={ B={ LF* CR* E={ F={ G='\x00' | H=text } LF* CR* }* } | CRLF }*
obs_unstruct := A={ B={ LF* CR* E={ F=obs_utext LF* CR* }* } | FWS }*
obs_phrase := A=word B={ word | '.' | CFWS }
obs_phrase_list := A={ phrase | CFWS } D={ E=',' F={ phrase | CFWS }? }*
obs_FWS := A=WSP+ { CRLF WSP+ }*
obs_day_of_week := CFWS? B=day_name CFWS?
obs_day := A=CFWS? B=DIGIT C=DIGIT? CFWS?
obs_year := A=CFWS? B=TWO_DIGIT C=DIGIT* CFWS?
obs_hour := CFWS? B=TWO_DIGIT CFWS?
obs_minute := CFWS? B=TWO_DIGIT CFWS?
obs_second := CFWS? B=TWO_DIGIT CFWS?
obs_zone := 'UT' | 'GMT' | 'EST' | 'EDT' | 'CST' | 'CDT' | 'MST' | 'MDT' | 'PST' | 'PDT' | '[\x41-\x49]' | '[\x4b-\x5a]' | '[\x61-\x69]' | '[\x6b-\x7a]'
obs_angle_addr := CFWS? B='<' C=obs_route D=addr_spec E='>' CFWS?
obs_route := obs_domain_list B=':'
obs_domain_list := A={ B=CFWS | C=',' }* D='@' E=domain F={ G=',' CFWS? I={ J='@' K=domain }? }*
obs_mbox_list := A={ B=CFWS? C=',' }* D=mailbox E={ F=',' G={ mailbox | I=CFWS }? }*
obs_addr_list := A={ B=CFWS? C=',' }* D=address E={ F=',' G={ address | I=CFWS }? }*
obs_group_list := A={ B=CFWS? C=',' }+ CFWS?
obs_local_part := A=word B={ C='\.' D=word }*
obs_domain := A=atom B={ C='\.' D=atom }*
obs_dtext := obs_NO_WS_CTL | B=quoted_pair
obs_fields := A={ B=obs_return | C=obs_received | D=obs_orig_date | obs_from | obs_sender | obs_reply_to | obs_to | obs_cc | obs_bcc | obs_message_id | L=obs_in_reply_to | obs_references | obs_subject | obs_comments | obs_keywords | obs_resent_date | obs_resent_from | obs_resent_send | obs_resent_rply | obs_resent_to | obs_resent_cc | obs_resent_bcc | obs_resent_mid | obs_optional }*
obs_orig_date := A='Date' WSP* C=':' D=date_time CRLF
obs_from := A='From' WSP* C=':' D=mailbox_list CRLF
obs_sender := A='Sender' WSP* C=':' D=mailbox CRLF
obs_reply_to := A='Reply-To' WSP* C=':' D=address_list CRLF
obs_to := A='To' WSP* C=':' D=address_list CRLF
obs_cc := A='Cc' WSP* C=':' D=address_list CRLF
obs_bcc := A='Bcc' WSP* C=':' D={ E=address_list | F={ G={ H=CFWS? I=',' }* CFWS? } } CRLF
obs_message_id := A='Message-ID' WSP* C=':' D=msg_id CRLF
obs_in_reply_to := A='In-Reply-To' WSP* C=':' D={ phrase | msg_id }* CRLF
obs_references := A='References' WSP* C=':' D={ phrase | msg_id }* CRLF
obs_id_left := local_part
obs_id_right := domain
obs_subject := A='Subject' WSP* C=':' D=unstructured CRLF
obs_comments := A='Comments' WSP* C=':' D=unstructured CRLF
obs_keywords := A='Keywords' WSP* C=':' D=obs_phrase_list CRLF
obs_resent_from := A='Resent-From' WSP* C=':' D=mailbox_list CRLF
obs_resent_send := A='Resent-Sender' WSP* C=':' D=mailbox CRLF
obs_resent_date := A='Resent-Date' WSP* C=':' D=date_time CRLF
obs_resent_to := A='Resent-To' WSP* C=':' D=address_list E=CRLF
obs_resent_cc := A='Resent-Cc' WSP* C=':' D=address_list CRLF
obs_resent_bcc := A='Resent-Bcc' WSP* C=':' D={ E=address_list | F={ G={ H=CFWS? I=',' }* CFWS? } } CRLF
obs_resent_mid := A='Resent-Message-ID' WSP* C=':' D=msg_id CRLF
obs_resent_rply := A='Resent-Reply-To' WSP* C=':' D=address_list CRLF
obs_return := A='Return-Path' WSP* C=':' D=path CRLF
obs_received := A='Received' WSP* C=':' D=received_token* E=CRLF
obs_optional := A=field_name WSP* C=':' D=unstructured CRLF